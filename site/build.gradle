plugins {
  id 'org.springframework.boot' version '2.2.1.RELEASE'
}

apply plugin: 'war'
apply plugin: 'io.spring.dependency-management'

repositories {
  mavenCentral()
}

// pulls out properties from a global properties file
Properties env = new Properties()
File subPropertiesFile = new File("$projectDir/env.properties")
subPropertiesFile.withInputStream {
  env.load(it)
}

// define environment and application settings
def gradleVersion = env.GRADLE_VERSION
def applicationName = env.APPLICATION_NAME
def buildNumber = env.APPLICATION_BUILD_NUMBER
group = env.APPLICATION_GROUP
version = env.APPLICATION_VERSION
def javaVersion = JavaVersion.toVersion(env.JAVA_VERSION)
sourceCompatibility = javaVersion
targetCompatibility = javaVersion
compileJava.options.encoding = env.ENCODING

def archiveName = "${applicationName}##${version + '.' + buildNumber}"

def profiles

bootJar {
  archiveFileName = "${archiveName}.jar"
}

bootWar {
  archiveFileName = "${archiveName}.war"
}

test {
  useJUnitPlatform()

  // need to be able to pass through the active profile supplied in gradle task
  // systemProperty 'spring.profiles.active', profiles
}

configurations {
  developmentOnly
  runtimeClasspath {
    extendsFrom developmentOnly
  }
  compileOnly {
    extendsFrom annotationProcessor
  }
}

ext {
  CDTS_VERSION = "2.1.0"
  JUNIT_VERSION = "5.3.1"
  MAPSTRUCT_VERSION = '1.4.1.Final'
  MAPSTRUCT_BINDING_VERSION = '0.2.0'
  MOCKITO_VERSION = '2.18.3'
  RESTASSURED_VERSION = '4.1.2'
}

compileJava {
    options.compilerArgs += [
        '-Amapstruct.defaultComponentModel=spring'
    ]
}

dependencies {
  implementation project(':dtos')
  // implementation "ca.gc.gocwebtemplate:gocwebtemplate-core:$CDTS_VERSION"
  // implementation "ca.gc.gocwebtemplate:gocwebtemplate-core-base:$CDTS_VERSION"
  // implementation "ca.gc.gocwebtemplate:gocwebtemplate-core-spring:$CDTS_VERSION"

  implementation fileTree(include: ["*.jar"], dir: "libs")
  annotationProcessor "org.mapstruct:mapstruct-processor:$MAPSTRUCT_VERSION" // easier/faster DTOs
  annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"
  annotationProcessor "org.projectlombok:lombok" // annotations for getters/setters/ctors/equals/hashCode
  annotationProcessor "org.projectlombok:lombok-mapstruct-binding:$MAPSTRUCT_BINDING_VERSION"
  developmentOnly "org.springframework.boot:spring-boot-devtools" // better dev experience in bootRun
  implementation "nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect"
  implementation "org.mapstruct:mapstruct:$MAPSTRUCT_VERSION" // easier/faster DTOs
  implementation "org.springframework.boot:spring-boot-starter-actuator" // health checks
  implementation "org.springframework.boot:spring-boot-starter-thymeleaf" // health checks
  implementation "org.springframework.boot:spring-boot-starter-web" // allows for 'controllers'
  providedRuntime "org.springframework.boot:spring-boot-starter-tomcat" // set to 'providedRuntime' to allow both jar/war deploys
  testAnnotationProcessor "org.mapstruct:mapstruct-processor:$MAPSTRUCT_VERSION" // easier/faster DTOs
  testImplementation "org.mockito:mockito-junit-jupiter:$MOCKITO_VERSION"
  testImplementation "io.rest-assured:rest-assured-all:$RESTASSURED_VERSION" // easier REST testing
  testImplementation "io.rest-assured:spring-mock-mvc:3.3.0" // test mocks
	testImplementation ("org.springframework.boot:spring-boot-starter-test") // allows for SpringBootTest
}

// make sure your environment is set up correctly!
task checkEnvironment {
  doFirst {
    if(JavaVersion.current() != javaVersion) {
      throw new GradleException("Configured version of Java (${javaVersion}) is different than the host version (${JavaVersion.current()})")
    }

    if(gradle.gradleVersion != gradleVersion) {
      throw new GradleException("Configured version of Gradle (${gradleVersion}) is different than the one used in the build (${gradle.gradleVersion})")
    }

    println "Gradle: ${gradle.gradleVersion}"
    println "Spring Boot profiles: ${profiles}"
    println "Java: ${javaVersion} (${env.JAVA_VERSION})"
  }
}

compileJava.dependsOn checkEnvironment
